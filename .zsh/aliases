# Sudo
alias svim='sudo vim'
alias stail="sudo tail"
alias svs="sudo supervisorctl"
# Used to have alias become="sudo su -" but that is hard to complete.
# so now it's in ~/bin/. Hrrgh. The below sets up completion for it.
compdef _users become

# Package managers
# Sudo-ify apt/yum things if sourcing user is not root (which is a thing on eg
# containers)
if [[ $(whoami) != 'root' ]]; then
    alias apt-get='sudo apt-get'
    alias apt='sudo apt'
    alias apt-cache='sudo apt-cache'
    alias apt-file='sudo apt-file'
    alias yum='sudo yum'
fi
alias ash='apt show'
alias ai='apt install'
alias as='apt search'
alias au='apt update'
alias ap='apt purge'
alias dl='dpkg --list'
alias pi='pip install'
alias pie='pip install -e'
alias pir='pip install -r'
alias pu='pip uninstall'
alias pf='pip list'
alias pl='pf'
alias b='brew'
alias bi='brew install'
alias bs='brew search'
alias bin='brew info'

# Git
if [[ -e `which hub 2>/dev/null` && -e `which ruby 2>/dev/null` ]]; then
    alias git=hub
    compdef hub=git
fi
alias gb='git branch'
alias gba='git branch -a'
alias gc='git commit'
alias gcm='git commit -m'
alias gcmn='git commit -n -m'
alias gca='git commit -a'
alias gcam='git commit -a -m'
alias gd='git diff'
alias gdc='git diff --cached'
alias gl='git log'
alias glg="git log --graph --pretty=format:'%Cblue%h%Creset -%C(yellow)%d%Creset %s %Cgreen(%ci)%Creset' --abbrev-commit"
alias gpl='git pull'
alias gps='git push'
alias gpsn='git push --no-verify'
alias gpsh='git push origin HEAD'
alias gpsf='git push -f origin HEAD'
alias gs='git status'
alias gsh='git show'
alias gt='git tag'
alias gr='git rel; echo ""; git wtf -r -L'
alias ga='git add'
alias gap='git add -p'
alias gco='git checkout'
alias nb='git checkout -b'
alias gst='git stash'
alias gsa='git stash apply'
alias gm='git merge'
alias gf='git fetch'
alias grs='git reset HEAD'
alias grsh='git reset HEAD~1'
alias gch='git cherry-pick'
alias gcx='git cherry-pick -enx'
alias amend='git commit --amend --no-edit'
alias amende='git commit --amend'


function gremote() {
    git remote add -f $1
    if [[ $? == 128 ]]; then
        git fetch $1
    fi
}

function gcamp() {
    gcam "$1" && gps
}

function get_branch() {
    export CURRENT_BRANCH=`git symbolic-ref --short HEAD`
}

function gu() {
    get_branch
    TARGET=${2:-master}
    METHOD=${1:-merge}
    gco "$TARGET" && gpl && gco "$CURRENT_BRANCH" && git $METHOD "$TARGET"
}

function nuke() {
    get_branch
    gco master && gpl && gb -d $CURRENT_BRANCH
}

function marge() {
    get_branch
    gco ${1:-master} && gm $CURRENT_BRANCH && gb -d $CURRENT_BRANCH
}

function mg() {
    find . \
        -mindepth 1 -maxdepth 2 \
        -type d -name .git \
    | while read git_dir; do
        dir=`dirname $git_dir`
        echo $dir:
        cd $dir
        git $*
        cd - >/dev/null
        echo ""
    done
}

# RubyGems
alias gems='gem search -b'
alias geml='gem list -l'
alias gemi='gem install -b --no-rdoc --no-ri'
alias gemu='gem uninstall'

# Python
alias rmv='deactivate; rmvirtualenv'

function mkv() {
    virtualenv $1
    source $1/bin/activate
    cd $1
}

# For arbitrary, non-venvwrapper-managed envs
function enter() {
    source $1/bin/activate
}

# Misc
alias tree='tree -Ca -I ".git|*.pyc|*.swp|dist|*.egg-info"'
alias rmpyc='rm **/*.pyc'
alias v=vim
alias be='bundle exec'
alias history='fc -l 1' # Force display of entire zsh history
alias l=ls
alias ports='sudo lsof -nP 2>/dev/null | grep LISTEN | sed -re "s/ +/ /g" | cut -d " " -f 1,3,9 | sort -u | column -t'

# Functions

# Prettyprint some JSON text with a few back-ends, w/ optional CURL:
# - 'json a-url-here' to curl-and-prettify
# - '<command emitting JSON> | json' to just-prettify
function json() {
    # Obtain prettyprinter command string
    if which jq > /dev/null; then
        # Default to using jq as prettyprinter, it's quite nice for that alone
        _pretty="jq -C ."
    else
        # Otherwise, fallback to Python's json module; Python is errywhere.
        _pretty="python -m json.tool"
        # Add pygmentize if it exists. (So, our absolute worst case is
        # pretty-printed, but not colorized. Still useful.)
        if which pygmentize > /dev/null; then
            _pretty="${_pretty} | pygmentize -l json"
        fi
    fi
    
    # Insert a curl if args were given; otherwise assume stdin is being used.
    if [[ -z $1 ]]; then
        eval ${_pretty}
    else
        eval "curl -ks $2 \"$1\" | ${_pretty}"
    fi | less -FR
}

# File pull request via 'hub', automatically inserting current HEAD as the
# source and the 'origin' remote account name as the source account.
# Useful for company-owned repos where hub's default assumptions don't work.
# TODO: make this usable in a forked-repo context
function pull() { # 'pr' already exists :(
    HEAD=`git symbolic-ref HEAD`
    # Probably a better way to do this. /lazy
    ACCOUNT=`git remote show -n origin | grep "Fetch URL" | cut -d ':' -f 3 | cut -d '/' -f 1`
    git push origin HEAD
    git pull-request -b ${1:-master} -h $ACCOUNT:${HEAD#refs/heads/} $2
}

# Fetch pull request X so its SHAs are available.
function fetch-pr() {
    git fetch origin pull/$1/head$2
}

# Create new local branch based off pull request X.
function pr-branch() {
    _NEWBRANCH=$1-int
    fetch-pr $1 :$_NEWBRANCH
    git checkout $_NEWBRANCH
    # NOTE: $current_git_branch is usually set by .zsh/prompt
    [[ $2 == "nomerge" ]] || git merge ${current_git_branch:-master}
}

# Stupid newline delimited => comma delimited bit (happens to ensure no
# trailing comma which is nice)
function nl2comma() {
    python -c "import sys; print ','.join(sys.stdin.read().splitlines())"
}

# Gnuplot junk
function asciigraph() {
    gnuplot -e "set term dumb; plot '-' with lines"
}

# Heh.
function _lolvim() {
    _msg="THIS AIN'T VIM, BUDDY"
    cowsay $_msg 2>/dev/null || echo $_msg
}
function :wq() { _lolvim }
function :qa() { _lolvim }

# Helpers to go from non-timestamped log output to something bin/logparse can
# profile. Expects stdin.
alias tss='ts -s "%H:%M:%.S"'
function profile() {
    tss | logparse $@
}
