#
# Colorized prompt, with different username colors for different systems.
#

# My specific palette, using zsh's builtin color expansions
typeset -A PALETTE C # Force zsh to treat as an array
PALETTE=(
    blue    33
    cyan    51
    green   82
    purple  171
    red     160
    yellow  227
    orange  167
)
# Wrap in %F{} and use short array name
for key in ${(k)PALETTE}; do
    C[$key]="%F{$PALETTE[$key]}"
done

# System => color/hostname map:
#   NAME: username color
#   CWD: location/cwd color
# Defaults:
NAME=$C[green]
CWD=$C[blue]

# Obtain hostname and domain parts as follows:
# - HOST: shortname of host, used to identify it as e.g. a workstation
# - HOSTNAME: display hostname, the actual text to show in prompt; frequently
#   just set to $HOST
# - DOMAIN: set to domain part, used to identify production systems etc

# If we seem to be on a DNS-name-less EC2 instance, use its instance ID
_id_file=/var/lib/cloud/data/instance-id
if [ ! `hostname -f 2>/dev/null` ] && test -e $_id_file; then
    HOST=`cat $_id_file`
    DOMAIN=
    HOSTNAME=$HOST
# Otherwise, assume FQDN provides deets
else
    HOST=`hostname -f | cut -d '.' -f 1`
    DOMAIN=`hostname -f | cut -d '.' -f 2-`
    HOSTNAME=`hostname`
fi

# Themed by domain name
case $DOMAIN in
    # Personal production
    bitprophet.org ) NAME=$C[cyan] ;;
    # Work production (actually everything...see .zsh/local for workstation
    # deets)
    *jumptrading.com ) NAME=$C[red] HOSTNAME=$HOST ;;
esac

# Themed by host/node name (takes precedence over domain)
# TODO: just look at .local once VMs all have that set
case $HOST in
    # Workstations
    callandor ) NAME=$C[yellow] CWD=$C[green] HOSTNAME=$HOST ;;
    # VMs
    vm* | debian* | ubuntu* ) NAME=$C[orange] CWD=$C[green] HOSTNAME=$HOST ;;
esac


function shortpath() {
    # Special vim-tab-like shortpath (~/folder/directory/foo => ~/f/d/foo)
    _pwd=`pwd | sed "s#$HOME#~#"`
    if [[ $_pwd == "~" ]]; then
        _dirname=$_pwd
    else
        _dirname=`dirname "$_pwd" | esed "s/\/(.)[^\/]*/\/\1/g"`
        if [[ $_dirname == "/" ]]; then
            _dirname=""
        fi
        _dirname="$_dirname/`basename "$_pwd"`"
    fi
    echo "${_dirname}"
}

function virtualenv_() {
    # Make sure it's cleaned out if we exit a virtualenv/project and don't
    # enter another one right away
    venv=""

    # Python virtualenv
    _venv=`basename "$VIRTUAL_ENV"`
    if [[ -n $_venv ]]; then
        venv=$_venv
    fi

    # Bundler bundle (Ruby)
    # This will 'overwrite' a virtualenv-driven $_venv if both exist, which
    # isn't often, lately
    # TODO: seek towards FS root in case in a subdir (not too common)
    _conf=".bundle/config"
    if [[ -f ${_conf} ]]; then
        # TODO: sincerely doubt anything portable & fast has YAML built in, but
        # maybe actually parse it sometime if I care hard enough
        _bundle_path="BUNDLE_PATH: /Users/jforcier/.bundles/"
        venv=`egrep "${_bundle_path}" ${_conf} | sed -e "s#${_bundle_path}##"`
    fi

    # Clojure (only if neither of the others)
    # Do dumb shit to avoid JVM hit :(
    # TODO: seek towards FS root in case in a subdir (not too common)
    if [[ -f project.clj && -z $_venv ]]; then
        _venv=`egrep -o "\(defproject ([^ ]+)" project.clj | sed -e "s/^(defproject //"`
        if [[ -n $_venv ]]; then
            venv=$_venv
        fi
    fi

    if [[ -n "$venv" ]]; then
        echo "$venv"
    else
        echo - -
    fi
}

function git_status() {
    _branch=$(git symbolic-ref HEAD 2>/dev/null)
    current_git_branch=${_branch#refs/heads/}
}

function dirty() {
    # Print nothing if not in a git repo
    [[ -z $current_git_branch ]] && return

    # Git branch / dirtiness
    # Dirtiness cribbed from:
    # http://henrik.nyh.se/2008/12/git-dirty-prompt#comment-8325834
    _dirty="*"
    if git update-index -q --refresh &>/dev/null; git diff-index --quiet --cached HEAD --ignore-submodules -- 2>/dev/null && git diff-files --quiet --ignore-submodules 2>/dev/null
        then _dirty=""
    fi
    echo $_dirty
}

function untracked() {
    [[ -z $current_git_branch ]] && return
    _untracked=""
    # Taken from oh-my-zsh/lib/git.zsh
    if git status --porcelain 2>/dev/null | grep '^??' &>/dev/null
        then _untracked="*"
    fi
    echo $_untracked
}

function branch() {
    if [[ -n "$current_git_branch" ]]; then
        echo "$current_git_branch"
    else
        echo - -
    fi
}

function displayhost() {
    # %m/%M seem broken for me? :(
    echo "$HOSTNAME"
}

#
# Prompt itself
#

function precmd() {
    # Set git vars
    git_status
    # [<green>time</green>] <NAME>HOST</NAME>:<CWD>pwd</CWD>
    #     {<magenta>venv</magenta>} [<magenta>branch</magenta>] <CWD>end</CWD>
    time="[$C[green]%T%f]"
    hostname="${NAME}$(displayhost)%f"
    _path="${CWD}%~%f"
    end="${CWD}Â»%f"
    export PS1="${time} ${hostname}:${_path} {$C[purple]$(virtualenv_)%f} [$C[purple]$(branch)$C[red]$(dirty)%f$C[yellow]$(untracked)%f]
${end} "
    export RPS1="%(?..$C[red]%?%f)"

    # Nuke mkvirtualenv_help
    unfunction mkvirtualenv_help 2>/dev/null
}
